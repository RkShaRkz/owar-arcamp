
var art_icon_lab, art_research_lab, art_icon_research, art_icon_use, art_research_progress, art_price;
var art_research_difficulty, art_observe_info, art_tele_countdown_in_progress;

Export Function InitArtifactVariables;
     begin
          art_icon_lab = [[],-1,0,0,0];  //If siberite lab with id 51 has the icon for researching/using artifact 3 then art_icon_lab[3] = 51. Index 1 holds the siberite labs that have any nearby artifact meaning tech_artifact can be researched.
          art_research_lab = [0,-1,0,0,0];  //If lab with id 46 is researching the technology for artifact number 4 then art_research_lab[4] = 46. Index 1 is tech_artifact.
          art_icon_research = [-1,-1,art_exp_left,art_exp_mid,art_exp_right];  //The icons used for researching the techs for each artifact.
          art_icon_use = [-1,-1,art_use_eye,art_use_sibexplosion,art_use_teleport];  //The icons used for using each artifact.
          art_research_progress = [-1,-1,0,0,0];  //Research progress of techs for each artifact.
          art_price = [-1,-1,3,8,50];  //Price in siberite crystals to use each artifact (in Am15a/Ru15a it is [[-1,-1,2,5,40],[-1,-1,3,8,50],[-1,-1,4,11,60]][difficulty]).
          art_research_difficulty = [-1,-1,150,300,500];  //How hard the different artifacts are to research (in Am15a/Ru15a it is [-1,-1,200,500,800]).
          art_observe_info = [];  //[x,y,time_left] - The places on the map the player has revealed with artifact 3 and for how much longer they are revealed before fading.
          art_tele_countdown_in_progress = false;  //True if the countdown to teleport units using artifact 5 is in progress.
     end;

//When the player spots one of the artifacts.
Every 0$1+0$0.7 do
     var i, hex, seen, temp_list, art;
     begin
          seen = false;
          art = 0;

          repeat
          wait(0$0.5);

          for i = 3 to 5 do
               begin
                    hex = FindArtifact(i);
                    if hex > 0 then
                         if SeeXY(you,hex[1],hex[2]) then
                              begin
                                   seen = true;
                                   art = i;
                                   break;
                              end;
               end;

          until seen;

          DialogueOn;

          CenterOnXY(hex[1],hex[2]);
          temp_list = UnitFilter([Olaf,Sonya,Kowalski],[f_ok]);
          if temp_list > 0 then
               begin
                    i = Rand(1,temp_list+0);
                    i = temp_list[i];
                    case i of
                         Olaf: Say(i,'D22-Ola-1');
                         Sonya: Say(i,'D22-So-1');
                         Kowalski: Say(i,'D22-Kow-1');
                    end;
               end;

          if art = 4 then
               ForceSay(Heike,'D22-H-1a')
          else
               ForceSay(Heike,'D22-H-1b');

          if not alliance_deal_status in [1,2] then
               begin
                    temp_list = UnitFilter([Sonya,Kowalski,Kurt,Kyouma],[f_ok]);
                    if temp_list > 0 then
                         begin
                              i = Rand(1,temp_list+0);
                              i = temp_list[i];
                              case i of
                                   Sonya: Say(i,'D22-So-2');
                                   Kowalski: Say(i,'D22-Kow-2');
                                   Kurt: Say(i,'D22-Ku-1');
                                   Kyouma: Say(i,'D22-Kyo-1');
                              end;
                         end
                    else
                         ForceSay(Heike,'D22-H-2');

                    SetTech(tech_artifact,you,state_enabled);
               end;

          DialogueOff;


          //Wait for Alliance to be destroyed and propose investigation of the artifacts.
          if GetTech(tech_artifact,you) <> state_enabled then
               begin
                    repeat
                    wait(0$1);
                    until not 2 in ai_bases;

                    DialogueOn;

                    temp_list = UnitFilter([Kowalski,Kurt,Kyouma],[f_ok]);
                    if temp_list > 0 then
                         begin
                              i = Rand(1,temp_list+0);
                              i = temp_list[i];
                              case i of
                                   Kowalski: Say(i,'D22-Kow-3');
                                   Kurt: Say(i,'D22-Ku-2');
                                   Kyouma: Say(i,'D22-Kyo-2');
                              end;
                         end
                    else
                         ForceSay(Heike,'D22-H-3');

                    DialogueOff;

                    SetTech(tech_artifact,you,state_enabled);
               end;
     end;


//When the player sees the Alien tower.
Every 0$1+0$0.5 do
     var i, temp_list;
     begin

          repeat
          wait(0$0.5);
          until See(you,alien_tower);

          DialogueOn;

          CenterOnUnits(alien_tower);

          temp_list = UnitFilter([Sonya,Oswald,Suze,MarkB],[f_ok]);
          if temp_list = 0 then
               temp_list = [Heike];

          i = Rand(1,temp_list+0);
          i = temp_list[i];
          case i of
               Sonya: Say(i,'D23-So-1');
               Oswald: Say(i,'D23-Os-1');
               Suze: Say(i,'D23-Su-1');
               MarkB: Say(i,'D23-Mark-1');
               Heike: ForceSay(i,'D23-H-1');
          end;

          ForceSay(Heike,'D23-H-2');

          if IsOk(Ralph) then
               Say(Ralph,'D23-Ra-1');

          temp_list = UnitFilter([Sonya,MarkB],[f_ok]);
          if temp_list = 0 then
               temp_list = [Heike];

          i = Rand(1,temp_list+0);
          i = temp_list[i];
          case i of
               Sonya: Say(i,'D23-So-2');
               MarkB: Say(i,'D23-Mark-2');
               Heike: ForceSay(i,'D23-H-3');
          end;

          DialogueOff;
     end;


//Enable/disable artifact techs depending on the proximity of the artifacts to
//a siberite lab.
//Control research of artifact techs.
//The player can research tech_artifact if there's one or more artifacts close to any sibetite lab.
//When tech_artifact is complete a siberite lab can research (and later use) artifacts close to it. Artifact
//number 5 must be researched and used in the alien tower, though, and can only be researched when the
//two other artifacts have been researched.
On ArtifactLoaded(cargo_bay,artifact) do
     var i, temp_list;
     begin
          ArtifactLoaded_AlliFunc(cargo_bay,artifact);

          if GetTech(tech_artifact,you) <> state_researched then
               begin
                    //Update labs with a nearby artifact.
                    art_icon_lab = Replace(art_icon_lab,1,[]);

                    for i = 3 to 5 do
                         ArtifactUnloaded_ArtiFunc(i);

                    if art_icon_lab[1] = 0 then
                         begin
                              ComCancel(art_research_lab[1]);
                              SetArtifactRes(you,false);
                         end;
               end
          else
               if art_icon_lab[artifact] > 0 or art_research_lab[artifact] > 0 then
                    begin
                         if art_research_progress[artifact] < 100 then
                              begin
                                   if art_research_lab[artifact] > 0 then
                                        ComCancel(art_research_lab[artifact]);

                                   if art_icon_lab[artifact] > 0 then
                                        SetArtifactUse(you,art_icon_research[artifact],art_no,art_icon_lab[artifact]);
                              end
                         else
                              if art_icon_lab[artifact] > 0 then  //should be the case
                                   SetArtifactUse(you,art_icon_use[artifact],art_no,art_icon_lab[artifact]);

                         art_icon_lab = Replace(art_icon_lab,artifact,0);
                    end;
     end;

On ArtifactUnloaded(cargo_bay,artifact) do
     begin
          ArtifactUnloaded_ArtiFunc(artifact);
     end;
Function ArtifactUnloaded_ArtiFunc(artifact);
     var sib_labs, i, loc, un, temp_list, alien_tower_close;
     begin
          loc = FindArtifact(artifact);
          if loc > 0 then
               begin
                    sib_labs = [];
                    alien_tower_close = false;
                                       
                    for i in GetHexesNearHex(loc[1],loc[2],3) do
                         begin
                              un = HexInfo(i[1],i[2]);
                              if un > 0 then 
                                   if GetType(un) = unit_building then
                                        begin
                                             if GetSide(un) = you and GetBType(un) in [b_lab_half,b_lab_full] and b_lab_siberium in [GetLabKind(un,1),GetLabKind(un,2)] and not IsConstructed(un) then
                                                  sib_labs = sib_labs ^ un;

                                             if un = alien_tower then
                                                  alien_tower_close = true;
                                        end;
                         end;
                         
                    if sib_labs > 0 or alien_tower_close then
                         begin
                              if GetTech(tech_artifact,you) <> state_researched then
                                   begin
                                        if sib_labs > 0 then
                                             begin
                                                  SetArtifactRes(you,true);

                                                  temp_list = art_icon_lab[1] union sib_labs;
                                                  art_icon_lab = Replace(art_icon_lab,1,temp_list);
                                             end;
                                   end
                              else
                                   begin
                                        if artifact = 5 then
                                             begin
                                                  //Only alien tower
                                                  if alien_tower_close and art_research_progress[3] = 100 and art_research_progress[4] = 100 then
                                                       sib_labs = [alien_tower]
                                                  else
                                                       sib_labs = [];
                                             end
                                        else
                                             begin
                                                  temp_list = sib_labs diff art_research_lab;  //Don't put research icon on labs researching stuff
                                                  sib_labs = [];
                                                  for i in temp_list do
                                                       if BuildingStatus(i) = bs_idle then
                                                            sib_labs = sib_labs ^ i;
                                             end;

                                        if art_research_progress[artifact] < 100 then
                                             begin
                                                  if sib_labs > 0 then
                                                       begin
                                                            SetArtifactUse(you,art_icon_research[artifact],art_instant,sib_labs[1]);
                                                            art_icon_lab = Replace(art_icon_lab,artifact,sib_labs[1]);
                                                       end;
                                             end
                                        else
                                             begin
                                                  if sib_labs > 0 then
                                                       begin
                                                            SetArtifactUse(you,art_icon_use[artifact],[-1,-1,art_place,art_unit,art_place][artifact],sib_labs[1]);
                                                            art_icon_lab = Replace(art_icon_lab,artifact,sib_labs[1]);
                                                       end;
                                             end;
                                   end;
                         end;
               end;
     end;

On ResearchStarted(tech,lab) do
     var i;
     begin
          ResearchStarted_AlliFunc(tech,lab);

          if tech = tech_artifact and GetSide(lab) = you then
               begin
                    art_research_lab = Replace(art_research_lab,1,lab);
               end;

          //Remove all icons from researching lab. Researching icons because we don't implement
          //queueing. Use-icons because apparently events "ArtifactUsed" won't trigger while the
          //lab is working.
          for i = 3 to 5 do
               if art_icon_lab[i] = lab then
                    begin
                         SetArtifactUse(you,art_icon_research[i],art_no,art_icon_lab[i]);
                         SetArtifactUse(you,art_icon_use[i],art_no,art_icon_lab[i]);
                         art_icon_lab = Replace(art_icon_lab,i,0);
                    end;
     end;

On ResearchCancel(tech,lab) do
     var i;
     begin
          ResearchCancel_AlliFunc(tech,lab);

          if tech = tech_artifact and GetSide(lab) = you then
               begin
                    art_research_lab = Replace(art_research_lab,1,0);
               end;

          if GetSide(lab) = you then
               begin
                    wait(3);  //wait for building status to update
                    for i = 3 to 5 do
                         ArtifactUnloaded_ArtiFunc(i);
               end;
     end;

On ResearchComplete(tech,lab) do
     var i, temp_list;
     begin
          ResearchComplete_AlliFunc(tech,lab);

          if tech = tech_artifact and GetSide(lab) = you then
               begin
                    art_research_lab = Replace(art_research_lab,1,0);

                    DialogueOn;

                    i = FindArtifactSciSpeaker;
                    if i > 0 then
                         begin
                              case i of
                                   Sonya: Say(i,'D24-So-1');
                                   MarkB: Say(i,'D24-Mark-1');
                                   Kyouma: Say(i,'D24-Kyo-1');
                                   else
                                        if GetSex(i) = sex_male then
                                             Say(i,'D24-SomeM-1')
                                        else
                                             Say(i,'D24-SomeF-1');
                              end;

                              ForceSay(Heike,'D24-H-1');

                              case i of
                                   Sonya: Say(i,'D24-So-2');
                                   MarkB: Say(i,'D24-Mark-2');
                                   Kyouma: Say(i,'D24-Kyo-2');
                                   else
                                        if GetSex(i) = sex_male then
                                             Say(i,'D24-SomeM-2')
                                        else
                                             Say(i,'D24-SomeF-2');
                              end;
                         end;

                    DialogueOff;
               end;

          if GetSide(lab) = you then
               begin
                    wait(3);  //wait for building status to update
                    for i = 3 to 5 do
                         ArtifactUnloaded_ArtiFunc(i);
               end;
     end;
Function FindArtifactSciSpeaker;
     var i, temp_list;
     begin
          temp_list = UnitFilter([Sonya,MarkB,Kyouma],[[f_ok],[f_class,class_scientistic]]);

          if temp_list = 0 then
               temp_list = UnitFilter([Sonya,MarkB,Kyouma],[f_ok]);

          if temp_list = 0 then
               temp_list = UnitFilter(GetNonmainPlayerCharacters,[[f_ok],[f_class,class_scientistic]]);

          if temp_list = 0 then
               temp_list = UnitFilter(GetNonmainPlayerCharacters,[f_ok]);

          if temp_list = 0 then
               result = 0
          else
               begin
                    i = Rand(1,temp_list+0);
                    result = temp_list[i];
               end;
     end;

On ArtifactUsed(side,art_icon,p1,p2) do
     var art_num, i, b, success, lab;
     begin
          ArtifactUsed_AlliFunc(side,art_icon);

          //Artifact research
          if art_icon in art_icon_research then
               begin
                    for art_num = 3 to 5 do
                         if art_icon = art_icon_research[art_num] then
                              break;

                    SetSpecResearch(art_icon_lab[art_num],art_research_difficulty[art_num],true);
                    SetWorkingProgress(art_icon_lab[art_num],art_research_progress[art_num]);
                    art_research_lab = Replace(art_research_lab,art_num,art_icon_lab[art_num]);

                    //Remove all icons from researching lab. Researching icons because we don't implement
                    //queueing. Use-icons because apparently events "ArtifactUsed" won't trigger while the
                    //lab is working.
                    for i = 3 to 5 do
                         if art_icon_lab[i] = art_research_lab[art_num] then
                              begin
                                   SetArtifactUse(you,art_icon_research[i],art_no,art_icon_lab[i]);
                                   SetArtifactUse(you,art_icon_use[i],art_no,art_icon_lab[i]);
                                   art_icon_lab = Replace(art_icon_lab,i,0);
                              end;
               end;

          //Artifact use
          if art_icon in art_icon_use then
               begin
                    for art_num = 3 to 5 do
                         if art_icon = art_icon_use[art_num] then
                              break;

                    if art_icon_lab[art_num] > 0 then  //should be the case
                         begin
                              if art_num = 5 and art_tele_countdown_in_progress then
                                   exit;

                              b = GetBase(art_icon_lab[art_num]);
                              if GetResourceType(b,mat_siberit) < art_price[art_num] then
                                   begin
                                        case art_num of
                                             3: Query('QInfoObserArtiSib');
                                             4: Query('QInfoDestruArtiSib');
                                             5: Query('QInfoTeleArtiSib');
                                        end;
                                   end
                              else
                                   begin
                                        case art_num of
                                             3: begin
                                                  PlaceSeeing(p1,p2,you,-15);
                                                  art_observe_info = Insert(art_observe_info,1,[p1,p2,0$10]);
                                                  SetResourceType(b,mat_siberit, GetResourceType(b,mat_siberit) - art_price[art_num] );
                                             end;
                                             4: begin
                                                  success = false;
                                                  case GetType(p1) of
                                                       unit_vehicle: begin
                                                            if GetEngine(p1) = engine_siberite or GetWeapon(p1) in [ru_time_lapser,ar_selfpropelled_bomb] then
                                                                 success = true;
                                                       end;
                                                       unit_building: begin
                                                            if GetBType(p1) in [b_siberite_power,b_ext_siberium,b_siberite_mine] or GetBWeapon(p1) = ru_time_lapser then
                                                                 success = true;
                                                       end;
                                                  end;

                                                  if success then
                                                       begin
                                                            KillUnit(p1);
                                                            SetResourceType(b,mat_siberit, GetResourceType(b,mat_siberit) - art_price[art_num] );
                                                       end
                                                  else
                                                       Query('QInfoDestruArtiUn');
                                             end;
                                             5: begin
                                                  //Countdown
                                                  SetResourceType(b,mat_siberit, GetResourceType(b,mat_siberit) - art_price[art_num] );
                                                  SetAreaMapShow(normal_teleport_area,1);
                                                  art_tele_countdown_in_progress = true;

                                                  for i = 10 downto 1 do
                                                       begin
                                                            display_strings = [i];
                                                            wait(0$1);
                                                       end;

                                                  display_strings = [];
                                                  SetAreaMapShow(normal_teleport_area,0);
                                                  art_tele_countdown_in_progress = false;

                                                  for i in FilterAllUnits([[f_side,you],[f_inarea,normal_teleport_area],[f_type,unit_vehicle]]) ^
                                                    FilterAllUnits([[f_side,you],[f_inarea,normal_teleport_area],[f_type,unit_human],[f_not,[f_driving]],[f_not,[f_inside]]]) do
                                                       begin
                                                            TeleportUnit(i,p1,p2,12,true);
                                                       end;
                                             end;
                                        end;
                                   end;
                         end;
               end;
     end;
//Remove seeing by artifact 3 after some time.
Every 0$1 do
     var i, time, temp_list;
     begin
          repeat
          wait(0$1);

          i = 1;
          while i <= art_observe_info do
               begin
                    time = art_observe_info[i][3] - 0$1;
                    if time <= 0$0 then
                         begin
                              RemoveSeeing(art_observe_info[i][1],art_observe_info[i][2],you);
                              art_observe_info = Delete(art_observe_info,i);
                         end
                    else
                         begin
                              temp_list = Replace(art_observe_info[i],3,time);
                              art_observe_info = Replace(art_observe_info,i,temp_list);
                              i = i + 1;
                         end;
               end;

          until false;
     end;

On SpecResearchCancel(lab,percentage_done) do
     var art;
     begin
          for art = 3 to art_research_lab do
               if art_research_lab[art] = lab then
                    begin
                         art_research_progress = Replace(art_research_progress,art,percentage_done);
                         art_research_lab = Replace(art_research_lab,art,0);

                         break;
                    end;

          wait(3);  //wait for building status to update
          for art = 3 to 5 do
               ArtifactUnloaded_ArtiFunc(art);
     end;

On SpecResearchComplete(lab) do
     var i, temp_list, art, k;
     begin
          for art = 3 to art_research_lab do
               if art_research_lab[art] = lab then
                    begin
                         art_research_progress = Replace(art_research_progress,art,100);
                         art_research_lab = Replace(art_research_lab,art,0);

                         if art_research_progress[3] = 100 and art_research_progress[4] = 100 and art_research_progress[5] = 100 then
                              all_artifacts_researched = true;

                         DialogueOn;

                         if art = 3 and IsOk(Kyouma) then
                              i = Kyouma
                         else
                              i = FindArtifactSciSpeaker;

                         if i > 0 then
                              begin
                                   case art of
                                        3: begin
                                             if i = Kyouma then
                                                  begin
                                                       Say(i,'D24-a-Kyo-1');

                                                       temp_list = UnitFilter([Sonya,MarkB],[f_ok]);
                                                       if temp_list > 0 then
                                                            begin
                                                                 k = Rand(1,temp_list+0);
                                                                 k = temp_list[k];

                                                                 ForceSay(Heike,'D24-a-H-1');

                                                                 case k of
                                                                      Sonya: Say(k,'D24-a-So-1');
                                                                      MarkB: Say(k,'D24-a-Mark-1');
                                                                 end;
                                                            end;
                                                  end;

                                             case i of
                                                  Sonya: Say(i,'D24-a-So-2');
                                                  MarkB: Say(i,'D24-a-Mark-2');
                                                  Kyouma: Say(i,'D24-a-Kyo-2');
                                                  else
                                                       if GetSex(i) = sex_male then
                                                            Say(i,'D24-a-SomeM-1')
                                                       else
                                                            Say(i,'D24-a-SomeF-1');
                                             end;

                                             ForceSay(Heike,'D24-a-H-2');

                                             if IsOk(Ralph) then
                                                  Say(Ralph,'D24-a-Ra-1');
                                        end;
                                        4: begin
                                             case i of
                                                  Sonya: Say(i,'D24-b-So-1');
                                                  MarkB: Say(i,'D24-b-Mark-1');
                                                  Kyouma: Say(i,'D24-b-Kyo-1');
                                                  else
                                                       if GetSex(i) = sex_male then
                                                            Say(i,'D24-b-SomeM-1')
                                                       else
                                                            Say(i,'D24-b-SomeF-1');
                                             end;

                                             ForceSay(Heike,'D24-b-H-1');

                                             case i of
                                                  Sonya: Say(i,'D24-b-So-2');
                                                  MarkB: Say(i,'D24-b-Mark-2');
                                                  Kyouma: Say(i,'D24-b-Kyo-2');
                                                  else
                                                       if GetSex(i) = sex_male then
                                                            Say(i,'D24-b-SomeM-2')
                                                       else
                                                            Say(i,'D24-b-SomeF-2');
                                             end;
                                        end;
                                        5: begin
                                             case i of
                                                  Sonya: Say(i,'D24-d-So-1');
                                                  MarkB: Say(i,'D24-d-Mark-1');
                                                  Kyouma: Say(i,'D24-d-Kyo-1');
                                                  else
                                                       if GetSex(i) = sex_male then
                                                            Say(i,'D24-d-SomeM-1')
                                                       else
                                                            Say(i,'D24-d-SomeF-1');
                                             end;

                                             ForceSay(Heike,'D24-d-H-1');

                                             case i of
                                                  Sonya: Say(i,'D24-d-So-2');
                                                  MarkB: Say(i,'D24-d-Mark-2');
                                                  Kyouma: Say(i,'D24-d-Kyo-2');
                                                  else
                                                       if GetSex(i) = sex_male then
                                                            Say(i,'D24-d-SomeM-2')
                                                       else
                                                            Say(i,'D24-d-SomeF-2');
                                             end;

                                             ForceSay(Heike,'D24-d-H-2');
                                        end;
                                   end;
                              end;

                         dwait(0$0.2);
                         case art of
                              3: begin
                                   Query('QInfoObserArtiUsage');

                                   dwait(0$0.2);
                                   if i > 0 and art_research_progress[3] = 100 and art_research_progress[4] = 100 then
                                        Art5CanResearchDialogue(i);
                              end;
                              4: begin
                                   Query('QInfoDestruArtiUsage');

                                   dwait(0$0.2);
                                   if i > 0 and art_research_progress[3] = 100 and art_research_progress[4] = 100 then
                                        Art5CanResearchDialogue(i);
                              end;
                              5: Query('QInfoTeleArtiUsage');
                         end;

                         DialogueOff;

                         break;
                    end;

          wait(3);  //wait for building status to update
          for art = 3 to 5 do
               ArtifactUnloaded_ArtiFunc(art);
     end;
Function Art5CanResearchDialogue(speaker);
     begin
          case speaker of
               Sonya: Say(speaker,'D24-c-So-1');
               MarkB: Say(speaker,'D24-c-Mark-1');
               Kyouma: Say(speaker,'D24-c-Kyo-1');
               else
                    if GetSex(speaker) = sex_male then
                         Say(speaker,'D24-c-SomeM-1')
                    else
                         Say(speaker,'D24-c-SomeF-1');
          end;
     end;