Export testing;

Export you, americans, nature, you_alt, you_alt_alt;

Export Heike, Sonya, Oswald, Ralph, Aviradze, Aviradze_standin, Kowalski, Willard, Evelyn, Gordon, Suze, MarkB, Olaf, Swansson;
Export Farmer, Farmer_vehicle, Abdul, Kurt, JMM;

Export siberite_time, bio_answer, Abdul_doubt, begging, first_ape_veh, abdul_convoy, sib_to_gather, sib_gathered, convoy_sent;
Export start_countdowns, player_depot_built, sib_techs_researched;

//American attack
Export attacking_drivers, attacking_soldiers_1, attacking_soldiers_2, attacking_soldiers_4, attacking_vehicles_1, attacking_vehicles_2, attacking_vehicles_3;
Export attacking_vehicles_4, attack_status, strat_1_hexes, strat_2_hexes, strat_3_hexes, strat_4_hexes, player_base_area, vehicle_pullback_hp;
Export attack_duration, soldier_pullback_hp, attacking_vehicles_5, strat_5_hexes, num_player_depots;

//Regarding siberite mini game
Export started, core_health, data_implemented, data_gathered, shutdown_time, game_duration, modifier_t, power_plant, implement_time;
Export first_power_plant, ticks_per_implementation, started_first_time, power_plant_sci, power_plant_depot;


Starting
begin                                   
     RandomizeAll;
     ResetFog;

     testing = false;  //Testing mission or real campaign?
                         
     //This is the base time the player has to mine the 250 siberite to get the medal. The calculations are based on the following data:
     //Without tech_sib1 a siberite mine produces 1 siberite in 399 ticks. With tech_sib1 it's 1 siberite in 285 ticks.
     //In a test by myself I built a siberite mine as first building after the depot (took a minute or so). About 5
     //minutes later I had researched tech_sib1. So in these 5 minutes I had mined 300*35 ticks / 399 tick/siberite
     //(= 26) siberite. Then I needed 250 - 26 (= 224) more which would take me 224 siberite * 285 tick/siberite
     //(= 1824 sec = 30.4 min).
     //Actually one would have to mine more siberite since you would use 5 on a siberite lab and 20 on a siberie power plant.
     //That yields an additional 25 siberite * 285 tick/siberite (= 204 sec = 3.4 min).
     //All in all it would take 5 + 30.4 + 3.4 = 38.8 minutes. This time must be greater than the max time the display string
     //can display with convoy time (the spot is marked with #-# (search it)).
     //Note that on hard difficulty you have to build two siberite mines to keep the schedule.
     sib_to_gather = 250;  //amount of siberite to gather
     siberite_time = Round(5$0 + (sib_to_gather+25 - 5$0/399) * 285 + [5$0,0$0,-5$0][difficulty]);

     sib_gathered = 0;  //the amount of siberite gathered when the convoy arrives
     bio_answer = 0;  //Answer for query - help Aviradze with research of apeman controlled vehicles or not.
     Abdul_doubt = LoadVariable('Abdul_doubt_07',false);  //Does Abdul doubt Heike can handle this mission?
     begging = LoadVariable('Begging_07',false);  //Did Heike beg Abdul to get this mission?
     first_ape_veh = true;  //turns false when first apeman controlled vehicle is constructed
     abdul_convoy = [];  //holds the vehicles in the convoy (to pick up the gathered siberite)
     convoy_sent = false; //turns true when Abdul has sent the convoy
     start_countdowns = false;  //true when countdown for American attack (module "AmericanAttack") + countdown to Abdul's convoy (module "Events") should begin
     player_depot_built = false;  //True when the player has built his/her a depot.
     sib_techs_researched = false;  //True when the player has researched all available siberite technologies

     attacking_drivers = [];  //Identifiers of the American drivers
     attacking_soldiers_1 = [];  //Identifiers of the attacking American soldiers using strategy 1
     attacking_soldiers_2 = [];  //Identifiers of the attacking American soldiers using strategy 2
     attacking_soldiers_4 = [];  //Identifiers of the attacking American soldiers using strategy 4

     attacking_vehicles_1 = [];  //The attacking American vehicles using strategy 1
     attacking_vehicles_2 = [];  //The attacking American vehicles using strategy 2
     attacking_vehicles_3 = [];  //The attacking American vehicles using strategy 3
     attacking_vehicles_4 = [];  //The attacking American vehicles using strategy 4
     attacking_vehicles_5 = [];  //The attacking American vehicles using strategy 5

     attack_duration = 4$0;  //The duration of the American attack
     attack_status = 0;  //0 for attack not begun, 1 for attack running, 2 for attack over

     strat_1_hexes = [];  //Hexes for strategy 1
     strat_2_hexes = [];  //Hexes for strategy 2
     strat_3_hexes = [];  //Hexes for strategy 3
     strat_4_hexes = [];  //Hexes for strategy 4
     strat_5_hexes = [];  //Hexes for strategy 5

     player_base_area = 0;  //Identifier of the area in which the player's base is (if any).
     vehicle_pullback_hp = 400;  //Vehicles with less HP will pull back
     soldier_pullback_hp = 450;  //Soldiers with less HP will pull back
     num_player_depots = 0;  //The number of depots/warehouses the player has when the attack begins

     first_power_plant = true;  //turns false after the first siberite power plant has been constructed (so dialogue only plays once)
     power_plant = 0;  //Identifier of siberite power plant
     power_plant_sci = 0;  //The scientist attending the power plant
     power_plant_depot = 0;  //The depot the power plant belongs to (assumed not to change, but only used to make the button)
     data_implemented = ToInteger(0);  //The amount of data implemented (percentage)


     PrepareSides;
     PrepareNature;
     PrepareArabians;
     PrepareAmericans;
     PrepareCrates;

     Action;
end;

Export Function Round(num);
     begin
          result = num div 1;
     end;

Function PrepareSides;
     begin
          //Who is who and decide initial attitudes
          nature = 0;
          you = 2;
          americans = 1;
          you_alt = 5;
          you_alt_alt = 8;  //necessary since if Farmer is present we share vision with you_alt

          SetAttitude(you,americans,att_enemy,true);
          SetAttitude(you,you_alt,att_friend,true);
          SetAttitude(you,you_alt_alt,att_friend,true);
          SetAttitude(americans,you_alt,att_friend,true);
          SetAttitude(americans,you_alt_alt,att_friend,true);
          SetAttitude(you_alt,you_alt_alt,att_friend,true);
     end;

Function PrepareNature;
     var i, spots, num, agres, count, temp_unit;
     begin
          spots = [ [12,7] , [29,5] , [33,22] , [21,25] , [32,36] , [26,43] , [51,32] , [70,24] , [61,4] , [84,11] , [93,5] ,
                    [106,10] , [114,25] , [124,38] , [123,46] , [133,55] , [124,58] , [135,68] , [142,80] , [123,77] ,
                    [99,51] , [87,44] , [90,71] , [66,57] , [36,62] , [126,95] , [74,35]
                  ];

          InitUc;
          InitHc;

          uc_side = nature;
          uc_nation = nation_nature;

          agres = -10 + difficulty * 20;

          //Apemen
          count = [8,7,6][difficulty];
          hc_class = class_apeman;
          for i = 1 to count do
               begin
                    if spots = 0 then
                         exit;

                    uc_direction = Rand(0,5);
                    
                    hc_agressivity = Rand(-1*agres,agres);

                    hc_attr = [Rand(11,13),Rand(10,11)];
                    hc_sex = Rand(sex_male,sex_female);
                    hc_skills = [Rand(0,1),Rand(0,1),0,0];

                    num = Rand(1,spots);
                    PlaceUnitXYR(CreateHuman,spots[num][1],spots[num][2],5,false);
                    spots = Delete(spots,num);
               end;

           //Tigers
           count = [2,3,4][difficulty];
           hc_gallery = 'nature_foreg';
           hc_face_number = 3;
           hc_class = class_tiger;
           hc_sex = sex_male;
           for i = 1 to count do
                begin
                     if spots = 0 then
                          exit;

                     uc_direction = Rand(0,5);
                     hc_agressivity = Rand(0,agres);
                     hc_attr = [Rand(11,12),Rand(10,11)];
                     hc_skills = [Rand(1,difficulty),0,0,0];

                     num = Rand(1,spots);
                     PlaceUnitXYR(CreateHuman,spots[num][1],spots[num][2],5,false);
                     spots = Delete(spots,num);
                end;

           //Phororhacos
           hc_class = class_phororhacos;
           hc_face_number = 1;
           hc_agressivity = -20;
           hc_skills = [0,0,0,0];
           for i = 1 to 6 do
                begin
                     if spots = 0 then
                          exit;

                     uc_direction = Rand(0,5);
                     hc_attr = [Rand(10,11),Rand(10,11)];
                     
                     num = Rand(1,spots);
                     PlaceUnitXYR(CreateHuman,spots[num][1],spots[num][2],5,false);
                end;
     end;

Function PrepareArabians;
     var human_list, vehicle_list, i, a;
     begin
          uc_side = you;
          uc_nation = nation_arabian;

          if not testing then
               begin
                    Heike = CreateCharacter('Heike_07_sel');
                    MarkB = CreateCharacter('MarkB_07_sel');

                    if TestCharacters('Aviradze_07_sel') then
                         begin
                              Aviradze = CreateCharacter('Aviradze_07_sel');
                              Aviradze_standin = 0;
                         end
                    else
                         begin
                              Aviradze_standin = CreateCharacter('Aviradze_standin_07_sel');
                              Aviradze = 0;
                         end;

                    if TestCharacters('Sonya_07_sel') then
                         Sonya = CreateCharacter('Sonya_07_sel')
                    else Sonya = 0;

                    if TestCharacters('Oswald_07_sel') then
                         Oswald = CreateCharacter('Oswald_07_sel')
                    else Oswald = 0;

                    if TestCharacters('Ralph_07_sel') then
                         Ralph = CreateCharacter('Ralph_07_sel')
                    else Ralph = 0;

                    if TestCharacters('Kowalski_07_sel') then
                         Kowalski = CreateCharacter('Kowalski_07_sel')
                    else Kowalski = 0;

                    if TestCharacters('Willard_07_sel') then
                         Willard = CreateCharacter('Willard_07_sel')
                    else Willard = 0;

                    if TestCharacters('Evelyn_07_sel') then
                         Evelyn = CreateCharacter('Evelyn_07_sel')
                    else Evelyn = 0;

                    if TestCharacters('Gordon_07_sel') then
                         Gordon = CreateCharacter('Gordon_07_sel')
                    else Gordon = 0;

                    if TestCharacters('Suze_07_sel') then
                         Suze = CreateCharacter('Suze_07_sel')
                    else Suze = 0;

                    if TestCharacters('Olaf_07_sel') then
                         Olaf = CreateCharacter('Olaf_07_sel')
                    else Olaf = 0;

                    if TestCharacters('Swansson_07_sel') then
                         Swansson = CreateCharacter('Swansson_07_sel')
                    else Swansson = 0;
               end
          else
               begin
                    InitHc;

                    PrepareSoldier(sex_female,6);
                    hc_name = 'Heike';
                    Heike = CreateHuman;

                    PrepareScientist(sex_male,6);
                    hc_name = 'Mark';
                    MarkB = CreateHuman;

                    PrepareScientist(sex_male,6);
                    hc_name = 'Aviradze';
                    Aviradze = CreateHuman;

                    Aviradze_standin = 0;

                    PrepareScientist(sex_female,6);
                    hc_name = 'Sonya';
                    Sonya = CreateHuman;

                    PrepareSoldier(sex_male,6);
                    hc_name = 'Oswald';
                    Oswald = CreateHuman;

                    PrepareSoldier(sex_male,6);
                    hc_name = 'Ralph';
                    Ralph = CreateHuman;

                    PrepareSoldier(sex_male,6);
                    hc_name = 'Kowalski';
                    Kowalski = CreateHuman;

                    PrepareMechanic(sex_male,6);
                    hc_name = 'Willard';
                    Willard = CreateHuman;

                    PrepareEngineer(sex_female,6);
                    hc_name = 'Evelyn';
                    Evelyn = CreateHuman;

                    Gordon = 0;

                    PrepareSoldier(sex_female,6);
                    hc_name = 'Suze';
                    Suze = CreateHuman;

                    PrepareMechanic(sex_male,6);
                    hc_name = 'Olaf';
                    Olaf = CreateHuman;

                    Swansson = 0;
               end;

          human_list = [MarkB,Sonya,Oswald,Ralph,Kowalski,Willard,Evelyn,Gordon,Suze,Olaf,Swansson] diff 0;
          human_list = human_list ^ (CreateCharacterSet('Others_07_sel') diff 0);

          for i in human_list do
               SetDir(i,[0,5][Rand(1,2)]);

          for i in [Heike,Aviradze,Aviradze_standin] do
               SetDir(i,0);

          //Two cargo bays with oil and crates
          InitVc;
          vc_chassis = ar_half_tracked;
          vc_engine = engine_combustion;
          vc_control = control_manual;
          vc_weapon = ar_cargo_bay;
          
          vehicle_list = [CreateVehicle,CreateVehicle];

          for i = 1 to vehicle_list do
               begin
                    SetFuel(vehicle_list[i],Rand(70,90));

                    a = Rand(1,human_list);
                    PlaceHumanInUnit(human_list[a],vehicle_list[i]);
                    human_list = Delete(human_list,a);
               end;

          SetCargo(vehicle_list[1],mat_cans,100);
          SetCargo(vehicle_list[2],mat_cans,[70,50,30][difficulty]);
          AddCargo(vehicle_list[2],mat_oil,[30,50,70][difficulty]);

          //Place units
          PlaceUnitXY(Heike,142,97,false);
          PlaceUnitXY(([Aviradze,Aviradze_standin] diff 0)[1],144,97,false);

          for i in vehicle_list ^ human_list do  //vehicles first
               PlaceUnitArea(i,enter_area,false);


          //Abdul and new characters
          Abdul = NewCharacter('Abdul');

          uc_side = you_alt;
          Farmer = NewCharacter('Farmer');
          if Abdul_doubt then
               begin
                    InitVc;
                    vc_chassis = ar_half_tracked;
                    vc_engine = engine_solar;
                    vc_control = control_manual;
                    vc_weapon = [ar_flame_thrower,ar_gun,ar_gatling_gun][Rand(1,3)];

                    Farmer_vehicle = CreateVehicle;
                    PlaceHumanInUnit(Farmer,Farmer_vehicle);

                    SetTech(tech_tech1,you_alt,state_researched);  //Faster solar power recharge
               end
          else
               Farmer_vehicle = 0;

          uc_side = you_alt_alt;
          Kurt = NewCharacter('Kurt');

          //Abdul's convoy to pick up the gathered siberite
          InitHc;
          InitVc;
          uc_direction = 0;
          vc_chassis = ar_half_tracked;
          vc_control = control_manual;
          vc_engine = engine_combustion;

          for i = 1 to 5 do
               begin
                    if i in [2,3,4] then
                         vc_weapon = ar_cargo_bay
                    else
                         vc_weapon = [ar_flame_thrower,ar_gun,ar_double_machine_gun,ar_multimissile_ballista,ar_gatling_gun][Rand(1,5)];

                    a = CreateVehicle;

                    if i = 1 then
                         PrepareMechanic(sex_male,6)
                    else
                         PrepareMechanic(false,6);

                    PlaceHumanInUnit(CreateHuman,a);

                    abdul_convoy = abdul_convoy ^ a;
               end;
     end;

Function PrepareAmericans;
     var i, f1, f2, f3, f4, f5, temp_list;
     begin
          //Macmillan
          uc_side = americans;
          JMM = NewCharacter('JMM');

          //Attack force
          f1 = [1,2,2][difficulty];
          f2 = 1;
          f3 = [1,1,2][difficulty];
          f4 = 1;
          f5 = [0,0,1][difficulty];

          attacking_drivers = CreateUnitsWithClass(f1+f2+f3+f4+f5,class_mechanic);

          attacking_vehicles_1 = CreateVehicles(f1,false);
          attacking_vehicles_2 = CreateVehicles(f2,false);
          attacking_vehicles_3 = CreateVehicles(f3,true);
          attacking_vehicles_4 = CreateVehicles(f4,false);
          attacking_vehicles_5 = CreateVehicles(f5,true);

          temp_list = attacking_vehicles_1 ^ attacking_vehicles_2 ^ attacking_vehicles_3 ^ attacking_vehicles_4 ^ attacking_vehicles_5;
          for i = 1 to attacking_drivers do
               begin
                    PlaceHumanInUnit(attacking_drivers[i],temp_list[i]);
                    SetTag(attacking_drivers[i],temp_list[i]);
               end;
                                                        
          attacking_soldiers_1 = CreateUnitsWithClass(difficulty,class_soldier);
          attacking_soldiers_2 = CreateUnitsWithClass(difficulty,class_soldier);
          //No soldiers created for strategy 4. Soldiers can only be transfered to this strategy.
     end;
Export Function CreateUnitsWithClass(num_units,un_class);
     var i, overskill;
     begin
          result = [];

          InitUc;
          InitHc;
          uc_side = americans;
          uc_nation = nation_american;

          overskill = difficulty + 3;

          for i = 1 to num_units do
               begin
                    if Rand(1,3) = 1 then
                         PrepareHuman(false,un_class,-overskill)
                    else PrepareHuman(false,un_class,overskill);

                    result = result ^ CreateHuman;
               end;
     end;
Export Function CreateVehicles(num,force_gun);
     var i;
     begin
          result = [];

          InitUc;
          InitVc;
          uc_side = americans;
          uc_nation = nation_american;
          vc_control = control_manual;
          vc_engine = engine_combustion;

          for i = 1 to num do
               begin
                    uc_direction = Rand(1,2);

                    if Rand(1,100) <= [20,40,60][difficulty] then
                         vc_chassis = us_heavy_tracked
                    else
                         vc_chassis = us_medium_tracked;

                    if Rand(1,100) <= [30,50,70][difficulty] or force_gun then
                         begin
                              if Rand(1,100) <= [30,50,70][difficulty] then
                                   vc_weapon = us_double_gun
                              else vc_weapon = us_light_gun;
                         end
                    else
                         if Rand(1,100) <= [30,50,70][difficulty] then
                              vc_weapon = us_gatling_gun
                         else vc_weapon = us_machine_gun;

                    result = result ^ CreateVehicle;
               end;
     end;

Function Action;
     begin
          InGameOn;
          wait(2);
          CenterNowOnXY(144,102);
          ComStand(all_units);

          if Aviradze > 0 then
               Say(Aviradze,'D2Av1')
          else Say(Aviradze_standin,'D2Avs1');

          Say(Heike,'D2H1');

          InGameOff;

          ComFree(all_units);

          wait(0$0.2);
          SaveForQuickRestart;

          wait(0$0.2);
          ChangeMissionObjectives('M1');
     end;